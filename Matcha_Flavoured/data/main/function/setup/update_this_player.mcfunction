#Revoke root adv, so that on_first_load functions run
advancement revoke @s only main:tutorial/root

# For a new version, we wipe their recipe unlock advancements so they can learn new things that were added or tweaked (or bugged)
advancement revoke @s from minecraft:recipes/root

# Remove any advancements that affect gameplay or reward you with money EXCEPT for fish everything and steal all prayers, because I think that might make people sad
advancement revoke @s from main:tutorial/light_campfire
advancement revoke @s from main:tutorial/obtain_estus
advancement revoke @s through main:tutorial/trade_everything
advancement revoke @s through main:hell/obtain_warding_stone
advancement revoke @s until main:hell/craft_blessing
advancement revoke @s from main:end/elytra

# Revoke Mechanics advancements which were not removed properly in previous versions
advancement revoke @s only main:mechanics/heart_container_obtained

# Maybe unnecessary? but if they have this they won't be able to sleep so just in case
advancement revoke @s only main:mechanics/slept_in_bed

#We remove old stopwatches that aren't used anymore
stopwatch remove minecraft:divinity15s
stopwatch remove minecraft:divinity20s
stopwatch remove minecraft:divinity30s

# As only players with a version number below the current version are made to run this function, we can set player's version number to current version
scoreboard players operation @s version_number = current_version version_number

# Announce that a player has been updated
tellraw @a ["",{"text":"[!]","bold":true,"color":"green"},{"text":": ","color":"green"},{"translate":"log.kleispack.player_updated","color":"gray"}]
